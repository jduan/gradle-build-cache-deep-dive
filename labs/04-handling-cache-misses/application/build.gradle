plugins {
    id 'application'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(':build-info')
    testImplementation 'junit:junit:4.12'
    testImplementation('com.github.stefanbirkner:system-rules:1.16.0') {
        exclude group: 'junit'
    }
}

mainClassName = 'com.example.application.Main'

test {
   def integrationTestSamples = file('src/integration-test-samples')
   environment.INTEGRATION_TEST_SAMPLES = integrationTestSamples.absolutePath
   // This makes sure that we take into account files under
   // "src/integrationTestSamples-test-samples" when running tests. Otherwise,
   // tests will pass when the test sample files change.
   inputs.dir(integrationTestSamples)
       .withPropertyName('integrationTestSamples')
       .withPathSensitivity(PathSensitivity.RELATIVE)
}

// This makes sure that "build-info.properties" is ignored when building
// "build-info", otherwise the task would be a cache miss every time because
// build-info.properties has a timestamp that keeps changing.
normalization {
   runtimeClasspath {
       ignore 'build-info.properties'
   }
}
